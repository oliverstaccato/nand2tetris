// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    //when inc=0, output doesn't change; when inc=1, output increment by 1
    Inc16(in=outreg, out=outi);  
    Mux16(a=outreg, b=outi, sel=inc, out=outinc);  

    //when load=0, output doesn't change; when load=1, out[t+1]=in[t]
    Mux16(a=outinc, b=in, sel=load, out=outload); 

    //when reset=0, output doesn't change; when reset=1, set output=0
    Mux16(a=outload, b=false, sel=reset, out=outreset);     
	
    //use the reseted output to start over
    Register(in=outreset, load=true, out=outreg, out=out);
}

